#!/usr/bin/env perl
use strict;
use warnings;
use FindBin qw'$Bin';
use lib "$FindBin::Bin";
use lib "$FindBin::Bin/perl_lib";
BEGIN { $ENV{PERL_JSON_BACKEND} = 0 }
use JSON -support_by_pp;
use Data::Dumper;
use File::Path qw(rmtree);
use Getopt::Long qw(GetOptions);

my $help;
my $conf_use_cached_src;
my $conf_nopackage;
my $ARCH = $ENV{ARCH};

#$conf_use_cached_src = 1;

GetOptions ("help|?" => \$help,
        "nofetch" => \$conf_use_cached_src,
        "nopackage" => \$conf_nopackage,
        "arch=s" => \$ARCH),
or die ("error in cmdline args\n");

if ($help) {
    print "Usage: $0 [options] [plugin_name(s)]\n";
    print "Options:\n";
    print "  --help                 Show this text\n";
    print "  --nofetch              Don't fetch package sources, use the cached copy\n";
    print "  --nopackage            Don't package the output files\n";
    print "  --arch=ARCH            i686 or x86_64\n";
    exit 0;
}

my $ORIGIN=$FindBin::Bin;
chdir($ORIGIN);
my $LIBDIR="";
my $CFLAGS="";
my $LDFLAGS="";
my $CONFIGURE_FLAGS;

if (!defined $ARCH) {
    die "Target arch is unspecified. Please use ARCH env variable, or --arch option\n";
}

if ($ARCH eq 'i686') {
    $LIBDIR="$ORIGIN/static-deps/lib-x86-32";
    $CFLAGS="-m32 -I$LIBDIR/include/i386-linux-gnu";
    $CONFIGURE_FLAGS = "--build=i686-unknown-linux-gnu";
    $LDFLAGS="-L$LIBDIR/lib/i386-linux-gnu";
}
elsif ($ARCH eq 'x86_64') {
    $LIBDIR="$ORIGIN/static-deps/lib-x86-64";
    $CFLAGS="-m64 -fPIC -I$LIBDIR/include/x86_64-linux-gnu";
    $CONFIGURE_FLAGS="--build=x86_64-unknown-linux-gnu";
    $LDFLAGS="-L$LIBDIR/lib/x86_64-linux-gnu";
    }
else {
    die "unknown arch $ARCH\n";
}

my $PLUGINFO="$ORIGIN/tools/pluginfo/$ARCH/pluginfo";

$ENV{ARCH} = $ARCH;

if (-e 'static-deps') {
    print "static-deps exists, delete for redownload\n";
}
else {
    print "downloading static-deps...\n";
    mkdir "temp";
    system ('wget http://sourceforge.net/projects/deadbeef/files/staticdeps/ddb-static-deps-latest.tar.bz2/download -O temp/ddb-static-deps.tar.bz2 && mkdir static-deps && tar jxf temp/ddb-static-deps.tar.bz2 -C static-deps') && die "failed to download static-deps\n";
}


my $AP="$ORIGIN/tools/apbuild";
`cd $AP; ./apinit`;
$ENV{CC}="$AP/apgcc";
$ENV{CXX}="$AP/apg++";
$ENV{APBUILD_STATIC_LIBGCC}=1;
$ENV{CFLAGS} = "$CFLAGS -I$LIBDIR/include";
$ENV{CXXFLAGS} = "$CFLAGS -I$LIBDIR/include";
$ENV{LDFLAGS} = "$LDFLAGS -L$LIBDIR/lib";
my $GTK_ROOT="$LIBDIR/gtk-2.12.12/usr";
my $GTK_ROOT_216="$LIBDIR/gtk-2.16.0";
my $GTK_ROOT_300="$LIBDIR/gtk-3.0.0";

my $GTK216_CFLAGS = "-I${GTK_ROOT_216}/include/gtk-2.0 -I${GTK_ROOT_216}/lib/gtk-2.0/include -I${GTK_ROOT_216}/include/atk-1.0 -I${GTK_ROOT_216}/include/cairo -I${GTK_ROOT_216}/include/pango-1.0 -I${GTK_ROOT_216}/include -I${GTK_ROOT_216}/include/glib-2.0 -I${GTK_ROOT_216}/lib/glib-2.0/include";

my $GTK216_LIBS="-L${GTK_ROOT_216}/lib -lgtk-x11-2.0 -lgdk-x11-2.0 -latk-1.0 -lgio-2.0 -lpangoft2-1.0 -lgdk_pixbuf-2.0 -lpangocairo-1.0 -lcairo -lpango-1.0 -lfreetype -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lrt -lglib-2.0 -lpthread";

my $GTK300_CFLAGS="-I${GTK_ROOT_300}/include/gtk-3.0 -I${GTK_ROOT_300}/include/pango-1.0 -I${GTK_ROOT_300}/include/gio-unix-2.0/ -I${GTK_ROOT_300}/include/atk-1.0 -I${GTK_ROOT_300}/include/cairo -I${GTK_ROOT_300}/include/gdk-pixbuf-2.0 -I${GTK_ROOT_300}/include/freetype2 -I${GTK_ROOT_300}/include/glib-2.0 -I${GTK_ROOT_300}/lib/glib-2.0/include";

my $GTK300_LIBS="-L${GTK_ROOT_300}/lib -lgtk-3 -lgdk-3 -latk-1.0 -lgio-2.0 -lpangocairo-1.0 -lgdk_pixbuf-2.0 -lcairo-gobject -lpango-1.0 -lcairo -lgobject-2.0 -lgthread-2.0 -lglib-2.0 -lfreetype -lpthread";

my %CLEAN_ENV = %ENV;

my @packages;

if (~~@ARGV) {
    @packages = @ARGV;
}
else {
    @packages = split (/\n/, `ls plugins`);
}

for my $package (@packages) {
    %ENV = %CLEAN_ENV;
    my $filename = "plugins/$package/manifest.json";
    next unless -e $filename;
    open F,"<$filename" or die "failed to read manifest for $package";
    my $manifest;
    {
        local $/;
        $manifest = <F>;
    }
    close F;

    my $json = JSON->new;
    $json->relaxed([1]);
    $json->allow_singlequote([1]);
    $json->allow_barekey([1]);

    $manifest = $json->decode($manifest);

    #print Dumper($manifest)."\n";
    if ($manifest->{disable}) {
        print STDERR "skipping $package because it's disabled in manifest\n";
        next;
    }

    if (!$manifest->{make}->{out} || !(~~@{$manifest->{make}->{out}})) {
        print STDERR "skipping $package because it doesn't define output\n";
        next;
    }


    my $ROOT = "temp/$package";
    print "getting source code using $manifest->{source}->{type} from $manifest->{source}->{url}\n";
    my $rev = 'unknown';

    if (!$conf_use_cached_src) {
        rmtree $ROOT;
        if ($manifest->{source}->{type} eq 'svn') {
            system ("svn checkout $manifest->{source}->{url} $ROOT") && die "svn checkout failed\n";
            $rev = `cd temp/$package && svn info | grep "Revision" | cut -c11-`;
        }
        elsif ($manifest->{source}->{type} eq 'git') {
            system ("git clone $manifest->{source}->{url} $ROOT") && die "git clone failed\n";
            if ($manifest->{source}->{revision}) {
                $rev = $manifest->{source}->{revision};
                system ("cd $ROOT && git checkout $rev") && die "git checkout $rev failed\n";
            }
            else {
                $rev = `cd temp/$package && git rev-parse --short HEAD`;
            }
        }
        elsif ($manifest->{source}->{type} eq 'hg') {
            system ("hg clone $manifest->{source}->{url} $ROOT") && die "hg clone failed\n";
            $rev = `cd temp/$package && hg log -r . --template '{node|short}'`;
        }
        print "\nsource code download success.\n";
        print "revision: $rev\n";
        chomp $rev;

        if (exists $manifest->{source}->{patches}) {
            print "\napplying patches\n";
            for my $p (@{$manifest->{source}->{patches}}) {
                system ("cd $ROOT/$manifest->{make}->{root}; patch -p1 <$ORIGIN/plugins/$package/$p") && die "patch failed\n";
            }
        }
    }

    if (exists $manifest->{source}->{exec} && !$conf_use_cached_src) {
        for my $e (@{$manifest->{source}->{exec}}) {
            print "running: $e\n";
            system "cd $ROOT/$manifest->{make}->{root}; $e";
        }
    }

    print "\npreparing environment...\n";

    if (exists $manifest->{make}->{ENV}) {
        for my $n (keys %{$manifest->{make}->{ENV}}) {
            my $v = eval("\"$manifest->{make}->{ENV}->{$n}\"");
            print "$n=$v\n";
            $ENV{$n} = $v;
        }
    }

    print "changing to: $ROOT/$manifest->{make}->{root}\n";
    print "\ninvoking make\n";
    if ($manifest->{make}->{type} eq 'make') {
        system ("cd $ROOT/$manifest->{make}->{root}; make clean") if $conf_use_cached_src;
        system ("cd $ROOT/$manifest->{make}->{root}; make") && die "make finished with an error\n";
    }
    elsif ($manifest->{make}->{type} eq 'autotools') {
        system ("cd $ROOT/$manifest->{make}->{root}; make distclean") if $conf_use_cached_src;
        if (exists $manifest->{make}->{bootstrap}) {
            system ("cd $ROOT/$manifest->{make}->{root}; $manifest->{make}->{bootstrap}") && die "$manifest->{make}->{bootstrap} finished with an error\n";
        }
        my $conf_flags = $CONFIGURE_FLAGS;
        if (exists $manifest->{make}->{configure_flags}) {
            $conf_flags .= ' '.$manifest->{make}->{configure_flags};
        }
        system ("cd $ROOT/$manifest->{make}->{root}; ./configure $conf_flags") && die "configure finished with an error\n";
        system ("cd $ROOT/$manifest->{make}->{root}; make clean") if $conf_use_cached_src;
        system ("cd $ROOT/$manifest->{make}->{root}; make") && die "make finished with an error\n";
    }
    else {
        die "make type '$manifest->{make}->{type}' is not supported\n";
    }

    next if $conf_nopackage;
    print "packaging $package...\n";

    # packaging
    # each plugin must list its output
    # the simple case is "out" list, that's for when plugin only consists of *.so

    # output is 1st copied to 'temp/plugins' folder,
    # then the plugins folder is zipped
    my $outdir = "temp/output/$ARCH/plugins";
    system ("rm -rf $outdir") && die "failed to remove $outdir\n";
    system ("mkdir -p $outdir") && die "failed to mkdir $outdir\n";
    for my $o (@{$manifest->{make}->{out}}) {
        my $fname = "$ROOT/$o";
        print "copying $fname to $outdir\n";
        system ("cp $fname $outdir") && die "failed to copy $o to $outdir\n";
    }

    # FIXME: support putting output into subdirs in 'plugins'
    
    # get plugin version, and generate descr file
    # this is needed e.g. for build-md script to find the right plugin zip
    my $ver;

    my $out = $manifest->{make}->{out};
    my $descr_fname = "temp/output/$ARCH/$package.descr";
    open F,">temp/output/$ARCH/$package.descr" || die "failed to open $descr_fname\n";

    # try loading all files from 'out', until we find one that works
    foreach (@$out) {
        $ENV{LD_LIBRARY_PATH} = "$GTK_ROOT_216/lib:$GTK_ROOT_300/lib:$LIBDIR/lib";
        print "LD_LIBRARY_PATH=$ENV{LD_LIBRARY_PATH}\n";
        my $config = `$PLUGINFO $ROOT/$_`;
        if (!$?) {
            my %conf_vars = parse_config ($config);
            $ver = $conf_vars{version};

            # write description file
            print F $config;
            last;
        }

    }
    if (!$ver) {
        $ver = 'unknown';
    }

    my $build = '';
    if ($manifest->{build}) {
        $build = "-$manifest->{build}";
    }

    my $out_fname = "$package-$rev$build-$ARCH.zip";
    print F "revision=\"$rev\"\n";
    print F "fname_$ARCH=\"$out_fname\"\n";
    close F;
    print "will pack $out_fname\n";
    unlink $out_fname;
    system ("cd temp/output/$ARCH && zip -r $out_fname plugins") && die "failed to zip $out_fname\n";
}

sub parse_config {
    $_ = shift;
    my %out;
    while (/([a-z_0-9]*)="([^"]*)"\n/g) {
        $out{$1} = $2;
    }
    return %out;
}
